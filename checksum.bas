100 ' <<<<< CHECK SUM  HUBASIC >>>>>
110 DEFINT A-Z:DIM V(7)
120 KEY0,"&H":INPUT"S-ADR.",S
130 KEY0,"&H":INPUT"E-ADR.",E
140 INPUT"PRINTER ON ? (Y/N)",K$
150 IF K$<>"Y" AND K$<>"N" THEN 140
160 P=(K$="Y"):Z=0:GOSUB 190
170 INPUT"RETRY ? (Y/N) ",K$
180 IF K$="Y" THEN 120 ELSE IF K$="N" THEN END ELSE 170
190 ' <<< MAIN >>>
200 GOSUB 410:FOR X=0 TO 7:V(X)=0:NEXT
210 FOR Y=0 TO 15:H=0:GOSUB 380
220 FOR X=0 TO 7:D=ASC(MEM$(S,1))
230 IF Z THEN P$="   ":GOSUB 400:GOTO 260
240 H=H+D:V(X)=V(X)+D:GOSUB 390
250 IF S=E THEN Z=1
260 S=S+1:NEXT:GOSUB 370
270 IF INKEY$=" " THEN K$=INKEY$(1)
280 IF Z THEN Y=15
290 NEXT
300 P$=STRING$(32,"-"):GOSUB 400:GOSUB 410
310 P$="SUM: ":GOSUB 400:H=0
320 FOR X=0 TO 7:D=V(X):H=H+D:GOSUB 390:NEXT
330 GOSUB 370
340 IF Z=0 THEN 190
350 GOSUB 410:RETURN
360 ' <<< SUB >>>
370 P$=":":GOSUB 400:D=H:GOSUB 390:GOTO 410
380 P$=RIGHT$("000"+HEX$(S),4)+" ":GOTO 400
390 P$=RIGHT$("0"+HEX$(D),2)+" "
400 PRINT P$;:IF P THEN LPRINT P$;:RETURN ELSE RETURN
410 PRINT:IF P THEN LPRINT:RETURN ELSE RETURN
