10 LIMIT &HE000:KEY1,"GOTO30"+CHR$(13)
20 PRINT"ⓒLOADING NOW...":LOADM "PIT",&HE000:BEEP
30 CLEAR:A$=INKEY$+INKEY$+INKEY$+INKEY$
40 REM ┌───────────┐
50 REM |   TITLE   |
60 REM └───────────┘
70 COLOR 7,0:CONSOLE:CLS:GOSUB 2610
80 A$=INKEY$(0):IF A$="" GOTO 80
90 IF A$="E" GOTO 1000
100 IF VAL(A$)>0 THEN L=VAL(A$)*5-1:GOTO 120
110 IF A$<>" " GOTO 80 ELSE L=0
120 M=5:CLS
130 REM ┌───────────┐
140 REM |   MAIN    |
150 REM └───────────┘
160 G=0:GB=0:IF L>49 THEN L=0
170 CONSOLE 0,25,33,7:COLOR 0,4:PRINT"ⓒ↓→LEVEL→→→↓";USING"###";L+1:PRINT"↓→LEFT↓→→→";MID$("    §§§§§",M,5)
180 CONSOLE
190 FOR X=0 TO 10:FOR Y=0 TO 7
200 Q=PEEK@ (&HE000+L*88+X+Y*11)
210 IF Q=2 THEN G=G+1
220 IF Q=6 THEN MX=X*3:MY=Y*3
230 LOCATE X*3,Y*3:ON Q+1 GOSUB 2410,2420,2430,2440,2450,2460,2470
240 NEXT:NEXT
259 LOCATE 0,24:COLOR 7,0:PRINT"█████████████████████████████████";
260 REM ┌──────────────┐
270 REM |   MAN MOVE   |
280 REM └──────────────┘
290 IF SY>1 THEN LOCATE MX,MY:GOSUB 2470
300 X=MX:Y=MY:X1=X:X2=X:SY=1:BEEP 0
310 IF CHARACTER$(X+1,Y+3)=" " AND GB=0 THEN MY=MY+3:GOTO 730
320 A$=INKEY$(0):IF A$="" GOTO 320
330 REM ┌─────────────────┐
340 REM |   MOVE (UP)     |
350 REM └─────────────────┘
360 IF A$<>"↑" OR Y<2 GOTO 390
370 A$=CHARACTER$(X+1,Y-3):BEEP 1
380 IF A$=" " OR A$="-" AND GB=1 THEN MY=MY-3:GOTO 730
390 IF A$<>"↓" GOTO 450
400 REM ┌─────────────────┐
410 REM |   MOVE (DOWN)   |
420 REM └─────────────────┘
430 A$=CHARACTER$(X+1,Y+3):BEEP 1
440 IF A$="-" OR A$=" " THEN MY=MY+3:GOTO 730
450 IF A$="→" THEN HX=3:BEEP 1:GOTO 600
460 IF A$="←" THEN HX=-3:BEEP 1:GOTO 600
470 IF A$="E" THEN GOTO 1000
480 IF A$="N" THEN L=L+1:GOTO 550
490 IF A$="B" THEN L=L-1:IF L<0 THEN L=49:GOTO 550 ELSE 550
500 IF A$="G" AND INKY$="0" GOTO 30
510 IF A$<>"G" GOTO 290
520 REM ┌─────────────────┐
530 REM |   GIVE (UP)     |
540 REM └─────────────────┘
550 M=M-1:IF M=0 THEN MUSIC"EEEDDCCRRRRR":GOTO 70
560 CONSOLE 0,24,0,33:COLOR ,0:CLS:GOTO 160
570 REM ┌───────────────────────┐
580 REM |   MOVE (LEFT/RIGHT)   |
590 REM └───────────────────────┘
600 IF X+HX<0 OR X+HX>30 GOTO 290
610 B$=CHARACTER$(X+HX+1,Y)
620 IF B$="▄" OR B$="-" OR B$="▒" OR B$=" " THEN MX=MX+HX:GOTO 730
630 IF X+HX*2<0 OR X+HX*2>30 GOTO 290
640 REM ┌────────────────┐
650 REM |   STONE PUSH   |
660 REM └────────────────┘
670 IF CHARACTER$(X+HX,Y)<>"◢" OR CHARACTER$(X+HX*2+1,Y)<>" " GOTO 290
680 LOCATE X+HX,Y:GOSUB 2410:LOCATE X+HX*2,Y:GOSUB 2450
690 IF HX=3 THEN X1=X+3:X2=X+6:SY=3 ELSE X1=X-6:X2=X-3:SY=2
700 REM ┌───────────────┐
710 REM |   PRINT MAN   |
720 REM └───────────────┘
730 BEEP 0:LOCATE X,Y:ON GB+1 GOSUB 2410,2420
740 A$=CHARACTER$(MX+1,MY):GB=0
750 IF A$="-" THEN GB=1
760 IF A$="▄" THEN G=G-1
770 LOCATE MX,MY
780 ON SY GOSUB 2470,2480,2490
790 REM ┌───────────────────────┐
800 REM |   STONE & GOLD FALL   |
810 REM └───────────────────────┘
820 Q=0
830 FOR X=X1 TO X2 STEP 3:FOR Y=21 TO 3 STEP -3
840 IF CHARACTER$(X+1,Y)<>" " GOTO 880
850 IF CHARACTER$(X+1,Y-3)="▄" THEN LOCATE X,Y:GOSUB 2430:GOTO 870
860 IF CHARACTER$(X+1,Y-3)="█" THEN LOCATE X,Y:GOSUB 2450 ELSE 880
870 LOCATE X,Y-3:GOSUB 2410:Q=1
880 NEXT:NEXT
890 IF Q>0 GOTO 820
900 IF G>0 GOTO 290
910 REM ┌─────────────────────┐
920 REM |   ONE LEVEL CLEAR   |
930 REM └─────────────────────┘
940 LOCATE 9,12:COLOR 7,0:PRINT"CONGRATULATIONS!";
950 MUSIC"CDE"
960 L=L+1:M=5:GOTO 160
970 REM ┌────────────┐
980 REM |   EDITOR   |
990 REM └────────────┘
1000 COLOR 7,1:PRINT"ⓒ  PITMAN EDITOR VER 1.0"
1010 PRINT">";
1020 A$=INKEY$(1)
1030 IF A$="H" GOTO 1150
1040 IF A$="P" GOTO 1290
1050 IF A$="L" GOTO 1360
1060 IF A$="S" GOTO 1430
1070 IF A$="C" GOTO 1500
1080 IF A$="E" GOTO 1590
1090 IF A$="D" GOTO 1860
1100 IF A$="R" GOTO 70
1110 BEEP:PRINT A$;" -ERROR":GOTO 1010
1120 REM ┌──────────┐
1130 REM |   HELP   |
1140 REM └──────────┘
1150 PRINT"HELP":PRINT
1160 PRINT"--- ..... ---"
1170 PRINT"H:HELP"
1180 PRINT"P:PLAY"
1190 PRINT"L:LOAD"
1200 PRINT"S:SAVE"
1210 PRINT"C:CLEAR"
1220 PRINT"E:EDIT"
1230 PRINT"D:DATA OUT/IN"
1240 PRINT"R:RETURN"
1250 PRINT:GOTO 1010
1260 REM ┌──────────┐
1270 REM |   PLAY   |
1280 REM └──────────┘
1290 PRINT:"PLAY"
1300 PRINT"PLAY NO.-";
1310 GOSUB 2570
1320 M=5:CLS:GOTO 160
1330 REM ┌──────────┐
1340 REM |   LOAD   |
1350 REM └──────────┘
1360 PRINT"LOAD":PRINT"SET TAPE. OK=[Y]";
1370 GOSUB 2530
1380 PRINT"PUSH PLAY."
1390 LOADM "PIT",&HE000:GOTO 1010
1400 REM ┌──────────┐
1410 REM |   SAVE   |
1420 REM └──────────┘
1430 PRINT"SAVE":PRINT"SET TAPE. OK=[Y]";
1440 GOSUB 2530
1450 PRINT"PUSH RECORD.PLAY"
1460 SAVEM "PIT",&HE000,&HF300:GOTO 1010
1470 REM ┌───────────┐
1480 REM |   CLEAR   |
1490 REM └───────────┘
1500 PRINT"CLEAR":PRINT"CLEAR NO.-";
1510 GOSUB 2570
1520 PRINT"OK?=[Y]";
1530 GOSUB 2530
1540 FOR Q=0 TO 87:POKE@ &HE000+L*88+Q,0:NEXT
1550 GOTO 1010
1560 REM ┌──────────┐
1570 REM |   EDIT   |
1580 REM └──────────┘
1590 PRINT"EDIT":PRINT"EDIT NO.-";
1600 GOSUB 2570
1610 COLOR ,1:CLS:FOR X=0 TO 10:FOR Y=0 TO 7
1620 Q=PEEK@ (&HE000+L*88+Y*11+X):LOCATE X*3,Y*3
1630 ON Q+1 GOSUB 2410,2420,2430,2440,2450,2460,2470
1640 NEXT:NEXT:X=0:Y=0:HX=0:HY=0
1650 LOCATE 35,1:COLOR 7,1:PRINT L+1;
1660 HX=0:HY=0
1670 LOCATE X*3,Y*3:COLOR 7,0:PRINT"┌→┐↓↓←←←└→┘";
1680 A$=INKEY$:IF A$="" GOTO 1680
1690 IF A$="↑" AND Y>0 THEN HX=0:HY=-1:GOTO 1780
1700 IF A$="↓" AND Y<7 THEN HX=0:HY=1:GOTO 1780
1710 IF A$="←" AND X>0 THEN HX=-1:HY=0:GOTO 1780
1720 IF A$="→" AND X<10 THEN HX=1:HY=0:GOTO 1780
1730 IF A$="N" THEN L=L+1:IF L>49 THEN L=0:GOTO 1610 ELSE 1610
1740 IF A$="B" THEN L=L-1:IF L<0 THEN L=49:GOTO 1610 ELSE 1610
1750 IF A$="R" GOTO 1000
1760 Q=VAL(A$):IF Q<1 OR Q>7 GOTO 1780
1770 POKE@ &HE000+L*88+X+Y*11,Q-1
1780 Q=PEEK@ (&HE000+L*88+X+Y*11)
1790 LOCATE X*3,Y*3
1800 ON Q+1 GOSUB 2410,2420,2430,2440,2450,2460,2470
1810 X=X+HX:Y=Y+HY
1820 GOTO 1660
1830 REM ┌─────────────────┐
1840 REM |   DATA OUT/IN   |
1850 REM └─────────────────┘
1860 PRINT"DATA OUT/IN"
1870 PRINT:PRINT"INPUT OUT(O)/IN(I)/RETURN(R)";
1880 B$=""
1890 A$=INKEY$(1)
1900 IF A$="O" PRINT:GOTO 1940
1910 IF A$="I" PRINT:GOTO 2150
1920 IF A$="R" PRINT:GOTO 1010
1930 GOTO 1890
1940 PRINT"DATA OUT NO.-";:GOSUB 2570
1950 FOR I=0 TO 86 STEP 2
1960 X1=PEEK@ (&HE000+L*88+I)
1970 IF X1=6 THEN X1=0
1980 X2=PEEK@ (&HE000+L*88+I+1)
1990 IF X2=6 THEN X2=0
2000 X=X1*6+X2
2010 IF X<10 THEN B$=B$+CHR$(X+48) ELSE B$=B$+CHR$(X+55)
2020 NEXT
2030 FOR I=0 TO 87
2040 X=PEEK@ (&HE000+L*88+I)
2050 IF X=6 THEN Q=I
2060 NEXT
2070 B$=B$+RIGHT$(STR$(Q),2)
2080 PRINT:PRINT"CODE:";B$:PRINT
2090 PRINT"SAVE TO TAPE YES=[Y]";
2100 A$=INKEY$(1);PRINT
2110 IF A$<>"Y" GOTO 1870
2120 PRINT"PUSH RECORD.PLAY"
2130 OPEN"O",1,"PIT":PRINT#1,B$:CLOSE
2140 GOTO 1870
2150 PRINT"INPUT FROM TAPE(T)/KEY(K)";
2160 A$=INKEY$(1):PRINT
2170 IF A$="T" GOTO 2340
2180 IF A$="K" GOTO 2200
2190 GOTO 1870
2200 INPUT"CODE:",B$
2210 IF LEN(B$)<>46 GOTO 2150
2220 PRINT"STORE NO.-";:GOSUB 2570
2230 FOR I=0 TO 43
2240 Q=ASC(MID$(B$,I+1,1))
2250 IF Q>47 AND Q<58 THEN Q=Q-48:GOTO 2280
2260 IF Q>64 AND Q<91 THEN Q=Q-55:GOTO 2280
2270 Q=0
2280 X1-INT(Q/6):X2=Q-X1*6
2290 POKE@ &HE000+L*88+I*2,X1,X2
2300 NEXT
2310 Q=VAL(RIGHT$(B$,2))
2320 POKE@ &HE000+L*88+Q,6
2330 GOTO 1870
2340 PRINT"PUSH PLAY."
2350 OPEN"I",#1,"PIT"
2360 INPUT#1,B$
2370 CLOSE:GOTO 2210
2380 REM ┌────────────────────┐
2390 REM |   3.3 CHARACTERS   |
2400 REM └────────────────────┘
2410 COLOR 7,0:PRINT"   ↓←←←   ↓←←←   ";:RETURN
2420 COLOR 7,0:PRINT"├─┤↓←←←├─┤↓←←←├─┤";:RETURN
2430 COLOR 6,0:PRINT" ▄ ↓←←←▐▞▌↓←←←███";:RETURN
2440 COLOR 2,0:PRINT"▒▒▒↓←←←▒▒▒↓←←←▒▒▒";:RETURN
2450 COLOR 2,0:PRINT"◢■◣↓←←←███↓←←←◥■◤";:RETURN
2460 COLOR 7,5:PRINT"┌──↓←←←(█)↓←←←──┘";:RETURN
2470 COLOR 7,0:PRINT" ● ↓←←← █)↓←←←┌┐ ";:RETURN
2480 COLOR 7,0:PRINT"╲● ↓←←←(█ ↓←←←─┐ ";:RETURN
2490 COLOR 7,0:PRINT" ●╱↓←←←   ↓←←← ┌─";:RETURN
2500 REM ┌──────────────────┐
2510 REM |   PUSH [Y] KEY   |
2520 REM └──────────────────┘
2530 A$=INKEY$(1):PRINT:IF A$="Y" RETURN ELSE RETURN 1010
2540 REM ┌────────────────┐
2550 REM |   INPUT 1-50   |
2560 REM └────────────────┘
2570 INPUT"",A$:L=VAL(A$)-1:IF L<0 OR L>49 RETURN 1010 ELSE RETURN
2580 REM ┌───────────────┐
2590 REM |   TITLE SUB   |
2600 REM └───────────────┘
2610 CONSOLE 2,12,0,40:COLOR 7,5:CLS
2620 FOR Y=3 TO 12
2630 LOCATE 3,Y:PRINT"██    ██   ██   ██  ██ ██ ██ ██ ██";
2640 NEXT
2650 FOR X=0 TO 4:RESTORE
2660 FOR Y=0 TO 7
2670 READ MX,MY
2680 LOCATE MX+X,MY:PRINT"█↓←█";
2690 NEXT
2700 LOCATE 6,3+X:PRINT"██";
2710 NEXT
2720 LOCATE 7,3:PRINT"◣↓↓↓↓↓↓←◤→→→→→→→→→→→→→↑↑↑↑↑↑◣◢↓↓↓←←◥◤→→→↑↑↑◢→→→◣→→→◣↓↓↓←◥";
2730 CONSOLE:COLOR 7,0
2740 LOCATE 8,16:PRINT"COPYRIGHT 1985 Y.ISOKAWA."
2750 FOR Y=18 TO 24
2760 READ A$:LOCATE 14,Y:PRINT A$;
2770 NEXT
2780 LOCATE 9,20:GOSUB 2430
2790 LOCATE 28,20:GOSUB 2430
2800 COLOR 7,0:RETURN
2810 REM ┌────────────────┐
2820 REM |   TITLE DATA   |
2830 REM └────────────────┘
2840 DATA 3,3,3,8,12,3,13,3,19,4,26,3,26,8,32,4
2850 DATA"┌───MENU───┐"
2860 DATA"|          |"
2870 DATA"| E:EDITOR |"
2880 DATA"|          |"
2890 DATA"| SPC:PLAY |"
2900 DATA"|          |"
2910 DATA"└──────────┘"
2920 REM
2930 REM
2940 REM ─┘¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯└─
2950 REM |          PITMAN          |
2960 REM |    DATE '85.3.27(WED)    |
2970 REM |       BY Y.ISOKAWA.      |
2980 REM ─┐________________________┌─
2990 REM
3000 REM
